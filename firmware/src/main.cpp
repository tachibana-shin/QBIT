// ==========================================================================
//  QBIT -- Firmware main (RTOS architecture)
// ==========================================================================

#include <Arduino.h>
#include <WiFi.h>
#include <ESPAsyncWebServer.h>
#include <U8g2lib.h>
#include <Wire.h>
#include <NetWizard.h>
#include <ESPmDNS.h>
#include <LittleFS.h>
#include <SD.h>
#include <new>

#include "app_state.h"
#include "settings.h"
#include "sd_manager.h"
#include "display_helpers.h"
#include "gif_player.h"
#include "web_dashboard.h"
#include "display_task.h"
#include "network_task.h"
#include "input_task.h"

#include "gif_types.h"
#include "sys_idle.h"

// ==========================================================================
//  Web server (shared by NetWizard and web dashboard)
// ==========================================================================

AsyncWebServer server(80);
NetWizard      NW(&server);

// ==========================================================================
//  Arduino setup()
// ==========================================================================

void setup() {
    Serial.begin(115200);
    Serial.setDebugOutput(false);

    // 1. NVS + pin config
    settingsInit();
    pinMode(getPinTouch(), INPUT);
    pinMode(getPinBuzzer(), OUTPUT);

    // 2. Display (reconstruct U8G2 with NVS pins)
    new (&u8g2) U8G2_SSD1306_128X64_NONAME_F_HW_I2C(
        U8G2_R0, U8X8_PIN_NONE, getPinSCL(), getPinSDA());
    u8g2.setBusClock(400000);
    u8g2.begin();
    clearFullGDDRAM();
    setDisplayInvert(false);

    // 3. Load settings + apply brightness
    loadSettings();
    setDisplayBrightness(getDisplayBrightnessVal());

    // 4. Create RTOS primitives
    gestureQueue      = xQueueCreate(8, sizeof(GestureEvent));
    networkEventQueue = xQueueCreate(16, sizeof(NetworkEvent));
    connectivityBits  = xEventGroupCreate();
    displayMutex      = xSemaphoreCreateMutex();
    gifPlayerMutex    = xSemaphoreCreateMutex();

    // 5. GIF player + idle animation
    gifPlayerInit(&u8g2);

    // 6. SD card -- mount and switch gif player storage if /QBit exists
    if (sdManagerInit()) {
        gifPlayerSetStorage(SD, "/QBit", "QBit/");
    } else {
        gifPlayerSetStorage(LittleFS, "/", "");
    }

    gifPlayerSetIdleAnimation(&sys_idle_gif);

    // 7. Start display task early so boot animation runs while WiFi and server init in parallel
    xTaskCreate(displayTask, "display", 8192, NULL, 2, NULL);

    // 8. NetWizard (NON_BLOCKING) with MAC-derived AP password
    String apPwd = getApPassword();

    NW.onConnectionStatus([](NetWizardConnectionStatus status) {
        if (status == NetWizardConnectionStatus::CONNECTED) {
            xEventGroupSetBits(connectivityBits, WIFI_CONNECTED_BIT);
        } else if (status == NetWizardConnectionStatus::CONNECTION_LOST ||
                   status == NetWizardConnectionStatus::DISCONNECTED) {
            xEventGroupClearBits(connectivityBits, WIFI_CONNECTED_BIT);
        }
    });

    NW.setStrategy(NetWizardStrategy::NON_BLOCKING);
    // Default NetWizard portal idle timeout is 5 minutes (no station on Soft AP), which then
    // tears down the AP and leaves the OLED stuck on "AP in 0s" until something reopens it.
    NW.setPortalTimeout(60UL * 60UL * 1000UL);   // 1 hour (ms) for first-time / captive setup
    // Hostname sent to router via DHCP
    {
        String hostname = "qbit-" + getDeviceId();
        hostname.toLowerCase();
        NW.setHostname(hostname.c_str());
    }
    NW.autoConnect("QBIT", apPwd.c_str());
    // Apply AP RF stability for ESP32-C3 PCB antenna after NetWizard (fixes #2). Does not change mode; AP_STA kept for portal.
    wifiApplyApRfStabilityForPcbAntenna();

    // 9. mDNS
    if (MDNS.begin("qbit")) {
        MDNS.addService("http", "tcp", 80);
    }

    // 10. Web dashboard + server
    webDashboardInit(server);
    // Wire cam WebSocket connect/disconnect → display task state machine
    webCamSetCallbacks(
        []() {
            NetworkEvent evt = {};
            evt.kind = NetworkEvent::CAM_START;
            BaseType_t ok = xQueueSend(networkEventQueue, &evt, pdMS_TO_TICKS(10));
            if (ok != pdPASS) {
                Serial.println("WARN: failed to enqueue CAM_START");
            }
        },
        []() {
            NetworkEvent evt = {};
            evt.kind = NetworkEvent::CAM_STOP;
            BaseType_t ok = xQueueSend(networkEventQueue, &evt, pdMS_TO_TICKS(10));
            if (ok != pdPASS) {
                Serial.println("WARN: failed to enqueue CAM_STOP");
            }
        }
    );
    server.begin();

    Serial.println("Web server started");

    // 11. Launch network and input tasks
    xTaskCreate(networkTask, "network", 8192, NULL, 1, NULL);
    xTaskCreate(inputTask,   "input",   2048, NULL, 3, NULL);
}

// ==========================================================================
//  Arduino loop() — all work done in RTOS tasks
// ==========================================================================

void loop() {
    vTaskDelete(NULL);
}

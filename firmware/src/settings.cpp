// ==========================================================================
//  QBIT -- NVS settings
// ==========================================================================
#include "settings.h"
#include "gif_player.h"

extern void weatherScreenInvalidateCache();

#include <Preferences.h>
#include <WiFi.h>
#include <freertos/FreeRTOS.h>
#include <freertos/semphr.h>

// ==========================================================================
//  Internal state
// ==========================================================================
static Preferences    _prefs;
static bool           _prefsReady = false;
static SemaphoreHandle_t _prefsMutex = NULL;

#define TZ_IANA_MAX_LEN 64

// GPIO pin defaults (ESP32-C3 Super Mini)
static uint8_t _pinTouch  = 1;
static uint8_t _pinBuzzer = 2;
static uint8_t _pinSDA    = 20;
static uint8_t _pinSCL    = 21;

// SD card pin defaults (custom SPI)
static uint8_t _pinSdCS   = 4;
static uint8_t _pinSdMOSI = 3;
static uint8_t _pinSdCLK  = 2;
static uint8_t _pinSdMISO = 1;

// Display
static uint8_t _brightness = 0x80;

// Buzzer
static uint8_t _buzzerVolume = 100;
static uint8_t _savedVolume  = 100;  // stored volume before mute

// Device
static String _deviceId;
static String _deviceName;

// MQTT
static String   _mqttHost;
static uint16_t _mqttPort    = 1883;
static String   _mqttUser;
static String   _mqttPass;
static String   _mqttPrefix;
static bool     _mqttEnabled = false;

// Timezone
static String  _tzIANA;


// Display orientation / GIF options
static bool _flipMode        = true;

// T-Rex Runner high score (stored under key "trexHi")
static uint32_t _trexHighScore   = 0;
static uint32_t _flappyHighScore = 0;
static uint32_t _carHighScore    = 0;
static bool _negativeGif     = false;

// Time format: true = 24h, false = 12h
static bool _timeFormat24h   = true;

// Weather factory defaults (used for static init and NVS fallbacks in loadSettings)
static const char     kWeatherCityDefault[] = "DIT Robotics";
static const char     kWeatherNameDefault[] = "DIT, Hsinchu";
static constexpr float kWeatherLatDefault  = 24.7949762f;
static constexpr float kWeatherLonDefault  = 120.9961819f;

static String  _weatherCity        = kWeatherCityDefault;
static float   _weatherLat         = kWeatherLatDefault;
static float   _weatherLon         = kWeatherLonDefault;
static String  _weatherDisplayName = kWeatherNameDefault;
static bool    _weatherManual      = false;

// ==========================================================================
//  Time format (24h/12h)
// ==========================================================================
bool getTimeFormat24h() { return _timeFormat24h; }
void setTimeFormat24h(bool val) { _timeFormat24h = val; }


// ==========================================================================
//  Device identity
// ==========================================================================

String getDeviceId() {
    if (_deviceId.length() == 0) {
        uint64_t mac = ESP.getEfuseMac();
        char id[13];
        snprintf(id, sizeof(id), "%04X%08X",
                 (uint16_t)(mac >> 32), (uint32_t)mac);
        _deviceId = String(id);
    }
    return _deviceId;
}

String getDeviceName() {
    return _deviceName;
}

void setDeviceName(const String &name) {
    _deviceName = name;
}

// ==========================================================================
//  AP password (derived from MAC — last 8 hex chars)
// ==========================================================================

String getApPassword() {
    uint64_t mac = ESP.getEfuseMac();
    char pwd[9];
    snprintf(pwd, sizeof(pwd), "%08X", (uint32_t)(mac & 0xFFFFFFFF));
    return String(pwd);
}

// ==========================================================================
//  Init & Load
// ==========================================================================

void settingsInit() {
    if (_prefsMutex == NULL) {
        _prefsMutex = xSemaphoreCreateMutex();
    }
    _prefs.begin("qbit", false);
    _prefsReady = true;

    // Read GPIO pin config first (needed before hardware init)
    _pinTouch  = _prefs.getUChar("pinTouch",  1);
    _pinBuzzer = _prefs.getUChar("pinBuzzer", 2);
    _pinSDA    = _prefs.getUChar("pinSDA",    20);
    _pinSCL    = _prefs.getUChar("pinSCL",    21);

    Serial.printf("GPIO pins: touch=%u buzzer=%u sda=%u scl=%u\n",
                  _pinTouch, _pinBuzzer, _pinSDA, _pinSCL);

    // Read SD card pin config
    _pinSdCS   = _prefs.getUChar("sdCS",   4);
    _pinSdMOSI = _prefs.getUChar("sdMOSI", 3);
    _pinSdCLK  = _prefs.getUChar("sdCLK",  2);
    _pinSdMISO = _prefs.getUChar("sdMISO", 1);

    Serial.printf("SD pins: cs=%u mosi=%u clk=%u miso=%u\n",
                  _pinSdCS, _pinSdMOSI, _pinSdCLK, _pinSdMISO);
}

void loadSettings() {
    if (!_prefsReady) {
        settingsInit();
    }
    if (xSemaphoreTake(_prefsMutex, portMAX_DELAY) != pdTRUE) return;

    _brightness   = _prefs.getUChar("bright", 0x80);
    _buzzerVolume = _prefs.getUChar("volume", 100);
    _savedVolume  = _buzzerVolume > 0 ? _buzzerVolume : 100;
    uint16_t speed = _prefs.getUShort("speed", 5);

    // Device name
    String defaultName = "QBIT-" + getDeviceId().substring(0, 4);
    _deviceName = _prefs.getString("devname", defaultName);

    // MQTT (truncate to limits in case stored value was from older code or direct NVS write)
    _mqttHost    = _prefs.getString("mqttHost", "");
    _mqttPort    = _prefs.getUShort("mqttPort", MQTT_PORT_DEFAULT);
    _mqttUser    = _prefs.getString("mqttUser", "");
    _mqttPass    = _prefs.getString("mqttPass", "");
    _mqttPrefix  = _prefs.getString("mqttPfx",  "qbit");
    _mqttEnabled = _prefs.getBool("mqttOn", false);
    if (_mqttHost.length() > MQTT_HOST_MAX_LEN)   _mqttHost = _mqttHost.substring(0, MQTT_HOST_MAX_LEN);
    if (_mqttUser.length() > MQTT_USER_MAX_LEN)   _mqttUser = _mqttUser.substring(0, MQTT_USER_MAX_LEN);
    if (_mqttPass.length() > MQTT_PASS_MAX_LEN)   _mqttPass = _mqttPass.substring(0, MQTT_PASS_MAX_LEN);
    if (_mqttPrefix.length() > MQTT_PREFIX_MAX_LEN) _mqttPrefix = _mqttPrefix.substring(0, MQTT_PREFIX_MAX_LEN);
    if (_mqttPrefix.length() == 0) _mqttPrefix = "qbit";
    if (_mqttPort < MQTT_PORT_MIN || _mqttPort > MQTT_PORT_MAX) _mqttPort = MQTT_PORT_DEFAULT;

    // Timezone (truncate if stored value exceeds limit)
    _tzIANA = _prefs.getString("tzName", "");
    if (_tzIANA.length() > TZ_IANA_MAX_LEN) {
        _tzIANA = _tzIANA.substring(0, TZ_IANA_MAX_LEN);
    }

    // Display / GIF options

    _flipMode        = _prefs.getBool("flipMode",  true);
    _negativeGif     = _prefs.getBool("negGif",    false);
    _timeFormat24h   = _prefs.getBool("time24h",   true);
    _trexHighScore   = _prefs.getUInt("trexHi",    0);
    _flappyHighScore = _prefs.getUInt("flappyHi",  0);
    _carHighScore    = _prefs.getUInt("carHi",     0);

    // Weather location (override via dashboard / NVS)
    _weatherCity        = _prefs.getString("wtCity", kWeatherCityDefault);
    if (_weatherCity.length() > WEATHER_CITY_MAX_LEN) _weatherCity = _weatherCity.substring(0, WEATHER_CITY_MAX_LEN);
    _weatherLat         = _prefs.getFloat("wtLat", kWeatherLatDefault);
    _weatherLon         = _prefs.getFloat("wtLon", kWeatherLonDefault);
    _weatherDisplayName = _prefs.getString("wtName", kWeatherNameDefault);
    if (_weatherDisplayName.length() > WEATHER_NAME_MAX_LEN) _weatherDisplayName = _weatherDisplayName.substring(0, WEATHER_NAME_MAX_LEN);
    if (_prefs.isKey("wtMan")) {
        _weatherManual = _prefs.getBool("wtMan", false);
    } else if (_prefs.isKey("wtCity") || _prefs.isKey("wtLat")) {
        // Legacy NVS had weather before pin flag existed — do not overwrite with IP.
        _weatherManual = true;
    } else {
        _weatherManual = false;
    }

    xSemaphoreGive(_prefsMutex);

    // Apply speed
    gifPlayerSetSpeed(speed);

    Serial.printf("Settings loaded: bright=%u vol=%u speed=%u\n",
                  _brightness, _buzzerVolume, speed);
    Serial.printf("Device ID: %s  Name: %s\n",
                  getDeviceId().c_str(), _deviceName.c_str());
    if (_mqttEnabled && _mqttHost.length() > 0) {
        Serial.printf("MQTT: %s:%u (prefix: %s)\n",
                      _mqttHost.c_str(), _mqttPort, _mqttPrefix.c_str());
    }
}

void saveSettings() {
    if (!_prefsReady) return;
    if (xSemaphoreTake(_prefsMutex, portMAX_DELAY) != pdTRUE) return;

    _prefs.putUShort("speed",    gifPlayerGetSpeed());
    _prefs.putUChar("bright",    _brightness);
    _prefs.putUChar("volume",    _buzzerVolume);
    _prefs.putString("devname",  _deviceName);
    _prefs.putString("mqttHost", _mqttHost);
    _prefs.putUShort("mqttPort", _mqttPort);
    _prefs.putString("mqttUser", _mqttUser);
    _prefs.putString("mqttPass", _mqttPass);
    _prefs.putString("mqttPfx",  _mqttPrefix);
    _prefs.putBool("mqttOn",     _mqttEnabled);
    _prefs.putUChar("pinTouch",  _pinTouch);
    _prefs.putUChar("pinBuzzer", _pinBuzzer);
    _prefs.putUChar("pinSDA",    _pinSDA);
    _prefs.putUChar("pinSCL",    _pinSCL);
    _prefs.putUChar("sdCS",      _pinSdCS);
    _prefs.putUChar("sdMOSI",    _pinSdMOSI);
    _prefs.putUChar("sdCLK",     _pinSdCLK);
    _prefs.putUChar("sdMISO",    _pinSdMISO);
    _prefs.putString("tzName",   _tzIANA);
    _prefs.putBool("flipMode",   _flipMode);
    _prefs.putBool("negGif",     _negativeGif);
    _prefs.putBool("time24h",    _timeFormat24h);
    _prefs.putUInt("trexHi",     _trexHighScore);
    _prefs.putUInt("flappyHi",   _flappyHighScore);
    _prefs.putUInt("carHi",      _carHighScore);
    // Weather location
    _prefs.putString("wtCity",   _weatherCity);
    _prefs.putFloat("wtLat",     _weatherLat);
    _prefs.putFloat("wtLon",     _weatherLon);
    _prefs.putString("wtName",   _weatherDisplayName);
    _prefs.putBool("wtMan",      _weatherManual);
    xSemaphoreGive(_prefsMutex);
    Serial.println("Settings saved to NVS");
}

// ==========================================================================
//  GPIO pin getters / setters
// ==========================================================================

uint8_t getPinTouch()  { return _pinTouch; }
uint8_t getPinBuzzer() { return _pinBuzzer; }
uint8_t getPinSDA()    { return _pinSDA; }
uint8_t getPinSCL()    { return _pinSCL; }
uint8_t getPinSdCS()   { return _pinSdCS; }
uint8_t getPinSdMOSI() { return _pinSdMOSI; }
uint8_t getPinSdCLK()  { return _pinSdCLK; }
uint8_t getPinSdMISO() { return _pinSdMISO; }

void setPinConfig(uint8_t touch, uint8_t buzzer, uint8_t sda, uint8_t scl) {
    if (!_prefsReady) return;
    if (xSemaphoreTake(_prefsMutex, portMAX_DELAY) != pdTRUE) return;
    _pinTouch  = touch;
    _pinBuzzer = buzzer;
    _pinSDA    = sda;
    _pinSCL    = scl;
    _prefs.putUChar("pinTouch",  _pinTouch);
    _prefs.putUChar("pinBuzzer", _pinBuzzer);
    _prefs.putUChar("pinSDA",    _pinSDA);
    _prefs.putUChar("pinSCL",    _pinSCL);
    xSemaphoreGive(_prefsMutex);
    Serial.println("Pin config saved -- rebooting...");
    delay(500);
    ESP.restart();
}

void setSdPinConfig(uint8_t cs, uint8_t mosi, uint8_t clk, uint8_t miso) {
    if (!_prefsReady) return;
    if (xSemaphoreTake(_prefsMutex, portMAX_DELAY) != pdTRUE) return;
    _pinSdCS   = cs;
    _pinSdMOSI = mosi;
    _pinSdCLK  = clk;
    _pinSdMISO = miso;
    _prefs.putUChar("sdCS",   _pinSdCS);
    _prefs.putUChar("sdMOSI", _pinSdMOSI);
    _prefs.putUChar("sdCLK",  _pinSdCLK);
    _prefs.putUChar("sdMISO", _pinSdMISO);
    xSemaphoreGive(_prefsMutex);
    Serial.println("SD pin config saved -- rebooting...");
    delay(500);
    ESP.restart();
}

// ==========================================================================
//  Display brightness
// ==========================================================================

void setDisplayBrightnessVal(uint8_t val) { _brightness = val; }
uint8_t getDisplayBrightnessVal() { return _brightness; }

// ==========================================================================
//  Buzzer volume
// ==========================================================================

void setBuzzerVolume(uint8_t pct) {
    _buzzerVolume = pct > 100 ? 100 : pct;
}

uint8_t getBuzzerVolume() { return _buzzerVolume; }

uint8_t getSavedVolume() { return _savedVolume; }
void    setSavedVolume(uint8_t vol) { _savedVolume = vol; }

// ==========================================================================
//  Flip mode
// ==========================================================================
bool getFlipMode()         { return _flipMode; }
void setFlipMode(bool val) { _flipMode = val; }

// ==========================================================================
//  Negative GIF
// ==========================================================================
bool getNegativeGif()         { return _negativeGif; }
void setNegativeGif(bool val) { _negativeGif = val; }

// ==========================================================================
//  Playback speed
// ==========================================================================

void setPlaybackSpeed(uint16_t val) { gifPlayerSetSpeed(val); }
uint16_t getPlaybackSpeed() { return gifPlayerGetSpeed(); }

// ==========================================================================
//  MQTT configuration
// ==========================================================================

String   getMqttHost()    { return _mqttHost; }
uint16_t getMqttPort()    { return _mqttPort; }
String   getMqttUser()    { return _mqttUser; }
String   getMqttPass()    { return _mqttPass; }
String   getMqttPrefix()  { return _mqttPrefix; }
bool     getMqttEnabled() { return _mqttEnabled; }

void setMqttConfig(const String &host, uint16_t port,
                   const String &user, const String &pass,
                   const String &prefix, bool enabled) {
    _mqttHost = host.length() > MQTT_HOST_MAX_LEN ? host.substring(0, MQTT_HOST_MAX_LEN) : host;
    _mqttPort = (port >= MQTT_PORT_MIN && port <= MQTT_PORT_MAX) ? port : MQTT_PORT_DEFAULT;
    _mqttUser = user.length() > MQTT_USER_MAX_LEN ? user.substring(0, MQTT_USER_MAX_LEN) : user;
    _mqttPass = pass.length() > MQTT_PASS_MAX_LEN ? pass.substring(0, MQTT_PASS_MAX_LEN) : pass;
    _mqttPrefix = prefix.length() > MQTT_PREFIX_MAX_LEN ? prefix.substring(0, MQTT_PREFIX_MAX_LEN) : prefix;
    if (_mqttPrefix.length() == 0) _mqttPrefix = "qbit";
    _mqttEnabled = enabled;
}

// ==========================================================================
//  Timezone
// ==========================================================================

String  getTimezoneIANA()  { return _tzIANA; }
void    setTimezoneIANA(const String &tz) {
    if (tz.length() > TZ_IANA_MAX_LEN) {
        _tzIANA = tz.substring(0, TZ_IANA_MAX_LEN);
    } else {
        _tzIANA = tz;
    }
}

// ==========================================================================
//  Game high scores
// ==========================================================================
// T-Rex Runner
uint32_t getTrexHighScore()          { return _trexHighScore; }
void     setTrexHighScore(uint32_t s) {
    if (s > _trexHighScore) {
        _trexHighScore = s;
        if (_prefsReady && xSemaphoreTake(_prefsMutex, portMAX_DELAY) == pdTRUE) {
            _prefs.putUInt("trexHi", _trexHighScore);
            xSemaphoreGive(_prefsMutex);
        }
    }
}

// Flappy Bird
uint32_t getFlappyHighScore()           { return _flappyHighScore; }
void     setFlappyHighScore(uint32_t s) {
    if (s > _flappyHighScore) {
        _flappyHighScore = s;
        // Persist immediately so it survives power-off
        if (_prefsReady && xSemaphoreTake(_prefsMutex, portMAX_DELAY) == pdTRUE) {
            _prefs.putUInt("flappyHi", _flappyHighScore);
            xSemaphoreGive(_prefsMutex);
        }
    }
}

// Car Avoidance
uint32_t getCarHighScore()           { return _carHighScore; }
void     setCarHighScore(uint32_t s) {
    if (s > _carHighScore) {
        _carHighScore = s;
        if (_prefsReady && xSemaphoreTake(_prefsMutex, portMAX_DELAY) == pdTRUE) {
            _prefs.putUInt("carHi", _carHighScore);
            xSemaphoreGive(_prefsMutex);
        }
    }
}

// ==========================================================================
//  Weather location
// ==========================================================================

String getWeatherCity()                  { return _weatherCity; }
void   setWeatherCity(const String &c)   { _weatherCity = c.length() > WEATHER_CITY_MAX_LEN ? c.substring(0, WEATHER_CITY_MAX_LEN) : c; }
float  getWeatherLat()                   { return _weatherLat; }
void   setWeatherLat(float lat)          { _weatherLat = lat; }
float  getWeatherLon()                   { return _weatherLon; }
void   setWeatherLon(float lon)          { _weatherLon = lon; }
String getWeatherDisplayName()           { return _weatherDisplayName; }
void   setWeatherDisplayName(const String &n) { _weatherDisplayName = n.length() > WEATHER_NAME_MAX_LEN ? n.substring(0, WEATHER_NAME_MAX_LEN) : n; }

void setWeatherLocation(float lat, float lon,
                        const String &city, const String &displayName) {
    _weatherLat         = lat;
    _weatherLon         = lon;
    _weatherCity        = city.length()        > WEATHER_CITY_MAX_LEN ? city.substring(0, WEATHER_CITY_MAX_LEN)               : city;
    _weatherDisplayName = displayName.length() > WEATHER_NAME_MAX_LEN ? displayName.substring(0, WEATHER_NAME_MAX_LEN) : displayName;
    if (_prefsReady && xSemaphoreTake(_prefsMutex, portMAX_DELAY) == pdTRUE) {
        _prefs.putFloat("wtLat",   _weatherLat);
        _prefs.putFloat("wtLon",   _weatherLon);
        _prefs.putString("wtCity", _weatherCity);
        _prefs.putString("wtName", _weatherDisplayName);
        xSemaphoreGive(_prefsMutex);
    }
    weatherScreenInvalidateCache();
}

bool getWeatherManual() { return _weatherManual; }

void setWeatherManual(bool manual) {
    _weatherManual = manual;
    if (_prefsReady && xSemaphoreTake(_prefsMutex, portMAX_DELAY) == pdTRUE) {
        _prefs.putBool("wtMan", _weatherManual);
        xSemaphoreGive(_prefsMutex);
    }
}
